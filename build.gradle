plugins {
    id 'java'
    id 'maven-publish'
}

def env = new Properties()
file("$rootDir/.env").withInputStream {
    env.load(it)
}

group = 'de.craftsblock.craftsnet.modules'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url = "https://repo.craftsblock.de/releases" }
    maven { url = "https://repo.craftsblock.de/experimental" }
}

dependencies {
    implementation platform("de.craftsblock.craftscore:bom:3.8.10")
    implementation "de.craftsblock.craftscore:event"
    implementation "de.craftsblock.craftscore:json"
    implementation "de.craftsblock.craftscore:utils"

    implementation "de.craftsblock:craftsnet:3.5.2"

    implementation "org.jetbrains:annotations:26.0.2"
}

publishing {
    publications {
        normal(MavenPublication) {
            artifactId = project.name.toLowerCase()

            from(components.java)

            pom {
                name = "CraftsNet-${project.name}"
                description = "Simple packet system for CraftsNet websockets"
                url = 'https://github.com/CraftsBlock/CraftsNet-WebSocketPackets'

                scm {
                    url = 'https://github.com/CraftsBlock/CraftsNet-WebSocketPackets'
                    connection = 'scm:git:git://github.com/CraftsBlock/CraftsNet-WebSocketPackets.git'
                    developerConnection = 'scm:git:git@github.com:CraftsBlock/CraftsNet-WebSocketPackets.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/CraftsBlock/CraftsNet-WebSocketPackets/issues'
                }

                licenses {
                    license {
                        name = 'GPL-3.0 license'
                        url = 'https://github.com/CraftsBlock/CraftsNet-WebSocketPackets/blob/master/LICENSE'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://repo.craftsblock.de/releases'
            authentication {
                basic(BasicAuthentication) {
                }
            }
            credentials {
                username = env["username"]
                password = env["password"]
            }
        }
    }
}